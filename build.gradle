plugins {
    id("groovy")
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.micronaut.application") version "4.2.1"
    id("io.micronaut.aot") version "4.2.1"
    id("io.micronaut.openapi") version "4.2.1"
    id("checkstyle")
    id("jacoco")
}

version = "1.0"
group = "com.cyolo"

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor("io.micronaut.validation:micronaut-validation-processor")
    annotationProcessor("io.micronaut.openapi:micronaut-openapi")
    implementation("io.micronaut.validation:micronaut-validation")
    implementation("io.micronaut:micronaut-http-server")
    implementation("io.micronaut:micronaut-management")
    implementation ("org.projectlombok:lombok")
    implementation("io.projectreactor:reactor-core:3.5.6")
    implementation('io.micronaut.micrometer:micronaut-micrometer-core:4.8.2')
    implementation('io.micronaut.micrometer:micronaut-micrometer-registry-prometheus:4.8.2')
    annotationProcessor ("org.projectlombok:lombok")
    compileOnly("io.micronaut:micronaut-http-client")
    annotationProcessor("io.micronaut.serde:micronaut-serde-processor")
    implementation("io.micronaut.serde:micronaut-serde-jackson")
    runtimeOnly("ch.qos.logback:logback-classic")
    testImplementation("io.micronaut:micronaut-http-client")
    runtimeOnly("org.yaml:snakeyaml")
}


application {
    mainClass.set("${servicePackage}.WordsFrequencyEvaluator")
}
java {
    sourceCompatibility = JavaVersion.toVersion("17")
    targetCompatibility = JavaVersion.toVersion("17")
}

checkstyle {
    configFile = file("${rootProject.projectDir}/config/checkstyle/wfe-checkstyle.xml")
    checkstyleMain.source = "src/main/java"
}

graalvmNative.toolchainDetection = false
micronaut {
    runtime("netty")
    testRuntime("spock2")
    processing {
        incremental(true)
        annotations("${servicePackage}.*")
    }
    openapi {
        server(file("${openapiSpecPath}")) {
            apiPackageName = "${servicePackage}.controller"
            modelPackageName = "${servicePackage}.DTO"
            useReactive = false
            useAuth = false
        }
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = "CLASS"
            excludes = ["com.cyolo.WordsFrequencyEvaluatorApp",
                        "com.cyolo.controller.mapper.*",
                        "com.cyolo.log.*",
                        "com.cyolo.DTO.*",
                        "com.cyolo.error.*",
                        "com.cyolo.model.*"]
            limit {
                counter = "LINE"
                minimum = 0.8
            }
        }
    }
}
check.dependsOn(jacocoTestCoverageVerification)



